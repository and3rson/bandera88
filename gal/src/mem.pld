GAL16V8
Mem.Deco

CLK    A16    A17    A18    A19   /M      MCB    NC     NC     GND
/OE    NC     NC    /HIRAM /LORAM /ROM    Q0     Q1     TMCK   VCC

;; Memory
; Low RAM:  $00000..$7FFFF
LORAM =   M  * /A19
; High RAM: $80000..($EFFFF if MCB==0 else $FFFFF)
HIRAM =   M  *  A19  * /ROM
; ROM:      $F0000..$FFFFF (disabled if MCB==1)
ROM   =   M  *  A16  *  A17  *  A18  *  A19  * /MCB

;; Q1 - CPU clock, inverted modulo-3 counter, output (Q1) is inverted with 74HC14
;; TMCK - 8253 timer clock, inverted modulo-2 counter of Q0, output (TMCK) is inverted with 74HC14
; +----+----+------+
; | Q0 | Q1 | TMCK |
; +----+----+------+
; | 1  | 1  | 1    |
; | 0  | 1  | 1    |
; | 0  | 0  | 1    |
; | 1  | 1  | 0    |
; | 0  | 1  | 0    |
; | 0  | 0  | 0    |
; +----+----+------+
Q0.R   = /Q0 * /Q1  ; Set to 1 only if Q0==0 && Q1==0
Q1.R   =  Q0 + /Q1  ; Set to 0 only if Q0==0 && Q1==1
TMCK.R = /Q0 * /Q1 * /TMCK + /Q0 *  Q1 *  TMCK +  Q0 * Q1 * TMCK  ; Flip if Q0==0 && Q1==0

DESCRIPTION
Memory address decoder for PK-88
